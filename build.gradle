plugins {
    id 'application'
    id 'java'
}

group = 'com.gamedev.towerdefense'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

def gdxVersion = '1.12.0'

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    runtimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'com.gamedev.towerdefense.DesktopLauncher'
}

tasks.test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('run', JavaExec) {
    jvmArgs = ['-XstartOnFirstThread']
}

task balance(type: JavaExec) {
    group = 'application'
    description = 'Runs the balance analyzer tool'
    mainClass = 'com.gamedev.towerdefense.util.BalanceAnalyzerTool'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ['-XstartOnFirstThread']
}

